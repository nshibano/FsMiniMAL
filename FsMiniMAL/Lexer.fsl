// After modify this file, run GenLexer.bat to generate Lexer.fs.
{
module FsMiniMAL.Lexer

open System
open System.Text
open Parser
open LexHelper
}

let re_ident = ['A'-'Z' 'a'-'z'] ['A'-'Z' 'a'-'z' '_' ''' '0'-'9']*
let re_float = ['0'-'9']+ ('.' ['0'-'9']*)? (['e' 'E'] ['+' '-']? ['0'-'9']+)?
let re_char_dec3 = '\\' ['0'-'9'] ['0'-'9'] ['0'-'9']
let re_char_hex4 = '\\' 'u' ['0'-'9' 'a'-'f' 'A'-'F'] ['0'-'9' 'a'-'f' 'A'-'F'] ['0'-'9' 'a'-'f' 'A'-'F'] ['0'-'9' 'a'-'f' 'A'-'F']

rule skip_sb =
  parse
  | "#!" [^ '\r' '\n'] * { () }
  | ""                   { () }

and main =
  parse
  | '\n'                                     { lexbuf.NewLine()
                                               main lexbuf }
  | [' ' '\r' '\t' '\012']+                  { main lexbuf }
  | re_ident                                 { ident_or_keyword lexbuf }
  | ['0'-'9']+
  | '0' ['x' 'X'] ['0'-'9' 'A'-'F' 'a'-'f']+
  | '0' ['o' 'O'] ['0'-'7']+
  | '0' ['b' 'B'] ['0'-'1']+                 { INT (lexeme_string lexbuf) }
  | re_float                                 { FLOAT (float (lexeme_string lexbuf)) }
  | "\""                                     { string lexbuf.StartPos (StringBuilder()) lexbuf }
  | "@\""                                    { verbatim_string lexbuf.StartPos (StringBuilder()) lexbuf }
  | "'"                                      { char_or_var lexbuf.StartPos lexbuf }
  | "(*"                                     { comment lexbuf.StartPos 1 lexbuf }
  | "&&"                                     { AMPERSANDAMPERSAND }
  | "||"                                     { BARBAR }
  | "!"                                      { EXCLAMATION }
  | "("                                      { LPAREN }
  | ")"                                      { RPAREN }
  | "*"                                      { STAR }
  | ","                                      { COMMA }
  | "->"                                     { MINUSGREATER }
  | "."                                      { DOT }
  | ":"                                      { COLON }
  | "::"                                     { COLONCOLON }
  | ";"                                      { SEMI }
  | "<-"                                     { LESSMINUS }
  | ":="                                     { COLONEQUAL }
  | "<<"                                     { LESSLESS }
  | "="                                      { EQUAL }
  | "=="                                     { EQUALEQUAL }
  | "["                                      { LBRACKET }
  | "[|"                                     { LBRACKETBAR }
  | "]"                                      { RBRACKET }
  | "_"                                      { UNDERSCORE }
  | "{"                                      { LBRACE }
  | "|"                                      { BAR }
  | "|]"                                     { BARRBRACKET }
  | "}"                                      { RBRACE }
  | "-" | "-."                               { SUBTRACTIVE (lexeme_string lexbuf) }
  | "~~~"                                    { TILDETILDETILDE }
  | "!="
  | "<>"
  | ['<' '>'] '=' ?                          { INFIX0(lexeme_string lexbuf) }
  | ['@' '^'] | "^^"                         { INFIX1(lexeme_string lexbuf) }
  | "+" | "+."                               { INFIX2(lexeme_string lexbuf) }
  | "%"
  | "*."
  | "/" | "/."
  | "&&&" | "|||"
  | "^^^"                                    { INFIX3(lexeme_string lexbuf) }
  | ">>>" | "<<<"                            
  | "**"                                     { INFIX4(lexeme_string lexbuf) }
  | "//"                                     { eol_comment lexbuf.StartPos lexbuf }
  | eof                                      { EOF }
  | _                                        { raise (Lexical_error Illegal_character) }

and comment start_pos depth = parse
  | '\n'    { lexbuf.NewLine()
              comment start_pos depth lexbuf }
  |  "(*"   { comment start_pos (depth + 1) lexbuf }
  | "*)"    { if depth = 1 then
                mark_as_comments lexbuf start_pos lexbuf.EndPos
                main lexbuf
              else
                comment start_pos (depth - 1) lexbuf }
  | eof     { mark_as_comments lexbuf start_pos lexbuf.StartPos
              raise(Lexical_error Unterminated_comment) }
  | _       { comment start_pos depth lexbuf }

and eol_comment start = parse
  | '\n'    { lexbuf.NewLine(); mark_as_comments lexbuf start lexbuf.EndPos; main lexbuf }
  | eof     { mark_as_comments lexbuf start lexbuf.StartPos; EOF }
  | _       { eol_comment start lexbuf }

and char_or_var start_pos = parse
  | "\n'"                               { lexbuf.NewLine()
                                          lexbuf.StartPos <- start_pos;
                                          CHAR (lexbuf.LexemeChar(0)) }
  | [^ '\\' '''] "'"                    { lexbuf.StartPos <- start_pos;
                                          CHAR (lexbuf.LexemeChar(0)) }
  | '\\' ['\\' ''' 'n' 't' 'b' 'r'] "'" { lexbuf.StartPos <- start_pos;
                                          CHAR ((char_for_backslash (lexbuf.LexemeChar(1)))) }
  | re_char_dec3 "'"                    { lexbuf.StartPos <- start_pos;
                                          CHAR (char_for_dec3_code lexbuf) }
  | re_char_hex4 "'"                    { lexbuf.StartPos <- start_pos;
                                          CHAR (char_for_hex4_code lexbuf) }
  | re_ident                            { lexbuf.StartPos <- start_pos;
                                          QUOTED (String(lexbuf.Lexeme)) }
  | eof                                  
  | _                                   { raise (Lexical_error Illegal_character) }

and string start_pos buf = parse
  | '\n'                                              { lexbuf.NewLine()
                                                        (buf : StringBuilder).Add('\n')
                                                        string start_pos buf lexbuf }
  | '"'                                               { lexbuf.StartPos <- start_pos
                                                        STRING ((buf : StringBuilder).ToString()) }
  | '\\' ("\010" | "\013" | "\013\010") [' ' '\009']* { string start_pos buf lexbuf }
  | '\\' ['\\' '"' 'n' 't' 'b' 'r']                   { (buf : StringBuilder).Add(char_for_backslash(lexbuf.Lexeme.[1]))
                                                        string start_pos buf lexbuf }
  | re_char_dec3                                      { (buf : StringBuilder).Add(char_for_dec3_code lexbuf);
                                                        string start_pos buf lexbuf }
  | re_char_hex4                                      { (buf : StringBuilder).Add(char_for_hex4_code lexbuf)
                                                        string start_pos buf lexbuf }
  | eof                                               { lexbuf.StartPos <- start_pos
                                                        raise (Lexical_error Unterminated_string) }
  | _                                                 { (buf : StringBuilder).Add(lexbuf.Lexeme.[0]);
                                                        string start_pos buf lexbuf }

and verbatim_string start_pos buf = parse
  | '\n'    { lexbuf.NewLine()
              (buf : StringBuilder).Add('\n')
              verbatim_string start_pos buf lexbuf }
  | '"' '"' { (buf : StringBuilder).Add('\034')
              verbatim_string start_pos buf lexbuf }
  | '"'     { lexbuf.StartPos <- start_pos
              STRING ((buf : StringBuilder).ToString()) }
  | eof     { lexbuf.StartPos <- start_pos;
              raise (Lexical_error Unterminated_string) }
  | _       { (buf : StringBuilder).Add(lexbuf.Lexeme.[0])
              verbatim_string start_pos buf lexbuf }